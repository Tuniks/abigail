title: PreFeltAzulejo
tags:
---
Chase: Abigail, you are back!
Abigail: Temporarily
Chase: Or forever! Who knows!
Abigail: Wow you... 
-> Look exactly the same, dude.
	Chase: So much for my new haircut...
	Abigail: I just mean, you look like you, is all.
-> Look so different.
	Chase: That's what happens when you run away to the big city...
    Abigail: Wow so you really miss me huh?
Oz: Ahoy! I'm here too! This must be the star of the good old days!
Chase: This is Oz, my friend from work
Abigail: Oh, hey
Chase: Why don't we grab a table and chat?
Abigail: Can't believe you are still coming here.
Chase: If it ain't broke, don't fix it
Chase: And last year they added a lava cake to the menu that's hella good, you should try it
Oz: Plus, Sarah is working today
Chase: Of course you have Sarah’s work schedule memorized.
Abigail: Who's Sarah?
Oz: Oh, she’s just this girl who… it’s kind of a long story
Chase: Anyways, Abbs, how's the big city treating you?
-> Some good, some bad.
-> Some bad, some good.
Oz: You are missing out then, here it's only good
Chase: They made bad illegal a couple years after you left
Abigail: Uh-huh. It shows.
Chase: C'mon, Abbs, we gotta get you in the Kiln mood!
Oz: Do you remember how to play Azulejo?
Abigail: I moved Oz, I didn't get lobotomized.
Chase: Let's play a match then!
Chase: And I'll judge! Let's see who here knows me the best. 
Abigail: Ok, ok. When in Kiln...
<<PreFeltAzulejoDone SceneController>>
===
title: PostFeltAzulejo
tags:
---
<<declare $tile_type = "" as String>>
<<LoadVariables WorldState>>
Oz: And that's another win for Oz!
Abigail: I'm using my mom's tiles, it's not fair!
Oz: I would have won even with your tiles, I know this boy too well.
Abigail: Ugh, I'm just rusty. Next time I'm destroying you.
Chase: Yes! That's my Abigail
Oz: Sorry guys, gotta dip, duty calls.
<<GoodbyebyeOz SceneController>>
Chase: Have fun at the mines!
Oz: I always do.
Abigail: Chase, do you remember this one tile, it had like a swirl on it? And it felt like
-> Holding someone’s hand
    <<set $tile_type to "hand">>
-> Saltwater in my hair
    <<set $tile_type to "saltwater">>
Chase: No? I don’t think so?
Abigail: I feel like I need to find it.
Chase: We could try looking at the antique shop
Chase: I’m headed over there right now to meet Kate, wanna come?
Abigail: My kate?
Chase: Yeah... Is it gonna be weird? m 
Abigail: No, no. Weird? Not at all. No.
Chase: Uh-huh. It shows.
Abigail: I don't know. Dani told me they'd be down in Wharf, so I might do that instead.
Chase: Sick, but I'm sure Kate would love to see you.
Chase: Either way, you're good.
Chase: If you wanna see Kate, the northwest bridge will lead you to Argil.
Chase: Otherwise take the path through the warehouses down south to Wharf.
Abigail: Yeah dude I know how to get around.
Chase: Oh, after your azulejo performance I thought you had erased us from your memory.
Abigail: I hate you, Chase.
Chase: I missed you, Abigail.
<<StoreVariables WorldState>>
<<PostFeltAzulejoDone SceneController>>
===
title: OutsideHouse2
tags:
---
Margarette: Hehehe
===
title: Vendor
tags:
---
<<if visited("Vendor") is false>>
Vendor:	I know you from somewhere.
Abigail: 	I know you from somewhere!
Vendor:	We went to tennis camp together.
-> I was really bad at tennis.
	Vendor:	You were really bad at tennis.
-> No, I don’t think that’s it.
	Vendor:	I have the crispest memory of your racket flying through the air after a fit of frustration.
	Vendor:	It soared beautifully.
Abigail: Now you sell Azulejo tiles?
Vendor:	Yeah my dad makes these.
Abigail: Whoa…is that you?
Vendor:	Yeah
Vendor:	...
Vendor:	My dad calls these ones “perfect son” tiles.
Abigail: That’s incredibly sweet.
Vendor:	And a little embarrassing.
Vendor: You're not allowed to buy one.
Abigail: My mom has socks with my face on them. 
Vendor:	...
Abigail: ...
Vendor:	Oh, ok. 
Abigail: I should go, see ya around.
<<else>>
Vendor: Socks...with your face.
<<endif>>
===
title: MomIntro
tags:
---
<<if visited("MomIntro") is false>>
Mom: As I live and breathe!
Dani: I didn't realize you were home. 
Abigail: Neither did I. I just woke up here. 
Mom: It's a sign!
Dani: Oh that happened to my cousin once.
Abigail: Wait really? Was it a round trip magical interlude or do I need to go buy a return ticket?
Dani: I don't really remember. We think it happened because he had a terrible case of athletes foot. 
Abigail: Aren't you supposed to be at that writing residency?
Dani: Do you have athletes foot Abigail?
Abigail: Shut up.
Mom: You hated that writing program anyway! A homecoming is always happy! Abigail come give me a hug baby. 
Abigail: This is a mess I need to get home.
Dani: So you are really busy in the city then?
Mom: Abigail calm down you look awful. I'm making tea!
Abigail: Ok, ok, fine. I'll have some tea.
Abigail: Is it from Stacy's?
Mom: Normally I would've gone and got you some from there but you never call. 
Abigail: Mom! I literally just woke up here. 
Dani: She doesn't call me either. 
Mom: This is from the place Dani and I like to go to.
Abigail: So you hang out with my mom a lot Dani?
Mom: They're my favorite person to have morning tea with! No one gossips with me like Dani does!
Dani: We were actually just talking about how we wish Mrs. Palmer was here for a game of Azulejo.
Mom: Forget Mrs. Palmer we have the Kiln Bay middle school champ of '83 in the house!
Abigail: Mom that's so embarassing. 
Dani: Let's do it your mom can judge. 
Mom: Abigail go grab some tiles from the chest so we can play.
<<MomIntroDone SceneController>>
<<else>>
Mom: Go on, Abigail, get the tiles from the chest! 
<<endif>>
===
title: Tourist
tags:
---
<<if visited("Tourist") is false>>
Tourist: These are absolutely blowing my mind! My heart is on fire! My soul is delighted!
Abigail: Yeah these tiles are pretty nice.
Tourist: All the blogs said that getting out of Acrylic Square is where you find the real genuine true authentic azulejo tiles. And oh boy were they right!
Abigail: The blogs?
Tourist: That’s how I heard about this city!
Tourist: Azulejo Addicts dot com! You can find it on the world wide web!
Abigail: Huh, yeah thanks I’ll look it up...
<<else>>
Tourist: My pals on the forums are gonna be soooo jealous.
<<endif>>
===
title: OutsideHouse
tags:
---

<<declare $talkedonce = false as bool>>
<<declare $talkedtwice = false as bool>>

<<if visited("OutsideHouse") is false and not $talkedonce>>
    Abigail: Good morning Mrs. Palmer.
    Margarette: Abigail, honey! I knew I would see you today!
    Abigail: Did my mom tell you I was here already?
    Margarette: No, that's not it.
    Margarette: I feel it in my joints when the city changes.
    Margarette: That's how I could tell you would be here.
    Abigail: Oh...
        -> I didn't know I was important enough to be felt in the joints.
             Margarette: Honey, we all are!
        -> And what else can your joints tell me?
             Margarette: That you can't wait to go back to the big city!
             Margarette: Okay, that one your mom did tell me.
             Margarette: Hehehe
    Margarette: But anyway, I don't mean to bother you too much.
    Margarette: A young lady like you must have places to go and people to see!
    Abigail: It was nice seeing you, Mrs. Palmer, I'll catch you later
    Margarette: I will be here waiting!
    <<set $talkedonce = true>>

<<elseif $talkedonce and not $talkedtwice>>
    Margarette: Abigail, honey, you know you are always in my prayers, right?
    Abigail: That's very nice of you, Mrs. Palmer.
    Margarette: Your mom tells me of all of your adventures out there!
    Abigail: All of them?
    Margarette: Every single one!
    Margarette: I am sorry about your last romantic encounter, by the way!
    Margarette: That must've been so embarrassing...
    Abigail: ...
    Margarette: Paolo almost choked laughing with that one!
    Abigail: Oh my god, Mr. Palmer too??
    Margarette: Of course!
    Margarette: Ever since the accident he needs all the distraction he can get!
    Abigail: ...
        -> I am going to assassinate my mother.
        -> I can't show my face around these parts anymore...
    Margarette: Hehehe
    <<set $talkedtwice = true>>

<<elseif $talkedtwice>>
    Margarette: Hehehe
<<endif>>
===
title: ClayStart
tags:
---
<<if visited("ClayStart") is false>>
Kate: Abigail? What are you doing here man?
Abigail: Kate. Hi. Wow. Yeah. I'm here. 
Chase: Words girl, use your words. 
Abigail: No, yeah. I just woke up here this morning. 
Abigail: You look really good. 
Chase: Too many words Abbs. 
Kate: Dude that's crazy I think the same thing happened to Dani's cousin once. 
Abigail: I don't have athletes foot!
Kate: ...
Chase: Abbs weren't you going on about a tile you were looking for?
Kate: You should check Miller's antique shop he always has weird tiles.
Abigail: Totally, I bet he'll have it. 
Kate: Come back and hang afterwords I haven't seen you in forever.
<<else>>
Chase: Do we think Miller made the building look like him?
Chase: Or did he make himself look like the building?
Kate: This feels like a chicken or the egg type of situation. 
<<endif>>
===
title: PostAntiqueShop
tags:
---
Chase: You find what you were looking for?
Abigail: Nope. 
Kate: Bummer dude. 
Abigail: Wasn't meant to be I guess. 
Kate: How’s the big city treating you these days?
Abigail: You know... really good. Really really good I'm having an awesome time. 
Kate: Yeah, totally.
Kate: Are you still working at that French restuarant?
Abigail: Not anymore French food is a little passé.
Abigail: How are...how is your life going?
Kate: It’s pretty good these days! I’m working at the library.
Kate: And I’ve been organizing that music festival on the beach, which is so much work but it's gonna be amazing.
Abigail: That’s amazing. You’re amazing.
Chase: Amazing!
Chase: What? I wanted to say it too.
Chase: Good adjective.
Kate: Maybe the best!
Chase:...
Abigail:...
Kate:...
Abigail: You know what, I was just telling Chase how much I wanted to play Azulejo, down for a round?
Chase: I thought you didn't like how they play it around here?
Kate: Yeah you used to say this one was too woo woo for you?
Abigail: No, must have me mixed up with someone else. 
<<PreClayAzulejoDone SceneController>>
===
title: AntiqueShopDay1
tags:
---
<<if visited("AntiqueShopDay1") is false>>
<<LoadVariables WorldState>>
Mr. Miller: Abigail! How's your mom? 
ShopAbigail: She's good Mr. Miller. 
Mr. Miller: She ever talk about me?
<<if $tile_type is "saltwater">>
    ShopAbigail: I'm looking for a specific Tile. It kinda feels like saltwater in your hair.
<<else>>
    ShopAbigail: I'm looking for a specific Tile. It kinda feels like holding someone's hand.
<<endif>>
Mr. Miller: I'm sure I have something like that might just need to do some digging. 
ShopAbigail: Take your time. 
Mr. Miller: I noticed you have your mom's old flower power tile. I made that for her way back when. 
ShopAbigail: How's finding that tile going Mr. Miller?
Mr. Miller: Aha! I bet this is the one right?
<<if $tile_type is "saltwater">>
    <<GiveSaltwaterTile SceneController>>
<<else>>
    <<GiveHandTile SceneController>>
<<endif>>
ShopAbigail: No I don't think so. 
Mr. Miller: Well go ahead and keep it and tell your mom old Mr. Miller says hello. 
ShopAbigail: Thank's for the tile Mr. Miller.
<<VisitAntiqueShop SceneController>>
<<else>>
Mr. Miller: Come back with your Mom if you want to buy something.
<<endif>>
===
title: PostClayAzulejo
tags:
---
<<if visited("PostClayAzulejo") is false>>
Abigail: I should probably get home.
Chase: Sick, we'll come with.
Abigail: Uh, why?
Chase: We’re going to the party at your house.
Kate: Your mom invited the whole gang.
Abigail: Tell me you're joking.
Kate: Aw man even Gabe?
Chase: He's still our friend even if you broke his heart.
Kate: Chase!
Abigial: Broke... his heart?
Chase: Don't worry we'll have plenty of time to finish this completely normal not weird at all conversation on the way to your house Abbs.
Abigail: I hate you I hate you I hate you.
<<PostClayAzulejoDone SceneController>>
<<else>>
Chase: We'll see you at your mom's, Abbs.
<<endif>>
===
title: WeirdHouse
tags:
---
<<if visited("WeirdHouse") is false>>
HOA President: I am just asking you, no begging you to listen to me!						
HOA President: STOP MAKING YOUR HOUSE LOOK STRANGER!						
HOA President: Every time I leave my house you’ve added a decoration, plant, trim, coat of paint.						
HOA President: It makes my head hurt! It makes my tummy upset!						
Linda: I don’t know what to tell you. It makes my heart soar!						
HOA President: Every other member of this neighborhood has no problem following the guidelines we’ve outlined?						
Linda: Oh sweet silly neighbor, comparisons are a waste of your time.		
<<else>>
Linda: Don't listen to what anyone says house, you look astonishing!
<<endif>>
===
title: PlaygroundKids
tags:
---
<<if visited("PlaygroundKids") is false>>
Ryan 1: Abigail hello!
Abigail: Hey, Ryan! What's up, buddy?
Ryan 2: Who's the old lady, Ryan?
Ryan 1: That's Abigail, she's my neighbor.
Abigail: was*, it's been a while since I moved away
Abigail: And also
Abigail: I'm not an old lady, I'm in my mid-twenties!
Ryan 2: That's old enough to be a grandmother
Abigail: No, it's not?
Ryan 1: I think I've seen grandmothers in their twenties
Abigail: No, You have not. Ryan, who is this clown?
Ryan 2: I'm Ryan
Abigail: No, HE is Ryan
Ryan 2: I'm Ryan too
Ryan 1: He's Ryan too
Ryan 2: Have you never seen two people with the same name?
Abigail: Oh... Is that why you two are friends? Same name?
Ryan 2: No? Do you pick your friends based on their names?
Ryan 1: We are friends because we both like the color purple
Ryan 2: And bugs
Ryan 1: Yeah bugs are awesome
-> I like purple too!
-> I like bugs too!
Ryan 2: Ok, grandma, no one asked
Abigail: Ok, I never asked to be tormented by babies, I'm leaving!
<<else>>
Ryan 1: So, what is your favorite bug?
Ryan 2: Every bug!
Ryan 1: Mine too!!
<<endif>>
===
title: HauntedHouseTeens
tags: 
---
<<if visited("HauntedHouseTeens") is false>>
Albert: I see a poem.
Fellini: I see a film.
Abigail: I see two losers. 
Albert: Abigail! Long time no see! 
Fellini: Art is cool lady. 
Abigail: Sorry, losers non-pejorative. 
Fellini: Pejorateive?
Albert: How've you been Abigail? Do you miss babysitting me?
Abigail: Not at all you were such a weird kid. 
Fellini: What does perjorative mean?
Albert: I was not a weird kid I was, I am, eclectic. 
Abigail: Whatever you say Albert I'll catch you later.
<<else>>
Fellini: Dude what does pejorative mean?
Albert: I don't know! Quit asking me. 
<<endif>>
===
title: KateChaseParty
tags:
---
<<if visited("KateChaseParty") is false>>
Kate: It's nice being back in your house Abigail. 
Abigail: Yeah um...
Abigail: It's nice having you back in my house. 
Kate: There's gonna be a really killer show tomorrow night at the warehouse, you should come.
Kate: Oh Chase! I just remembered I have a favor to ask.
Kate: Tomorrow at 11am we’re doing a big load in a day at the venue. 
Kate: We could use some extra hands to move equipment?
Chase: When is this?
Kate: Tomorrow at 11am.
Chase: Hmm maybe I could...
Chase: If you absolutely can’t find anyone else, let me know.
Abigail: You suddenly have a 9-5 or something Chase?
Chase: Me? Never.
Kate: His latest thing has been carpentry.
Chase: I’ve been feeling a lot like Jesus lately.
Abigail: You’ve never been to church in your life.
Chase: But I would look good on a cross!
Chase: Speaking of being shirtless, I'm trying to put together a beach day for Abbs' visit. 
Chase: I'm thinking day after tomorrow?
Kate: I have a shift at the library, but I'll see if I can switch it?
Abigail: That sounds great Chase. 
    <<set $peopletalkedto = $peopletalkedto + 1>>
    <<if $peopletalkedto > 2>>
        Gabe:Alright party people! Let's play some Azulejo.
        Joanna: Abigail should judge!
        <<StartPartyAzulejo PartyTransitionManager>>
    <<else>>
        Abigail: I should talk to rest of the gang.
    <<endif>>
<<else>>
    Chase: I can try repairing the bridhouse if you want?
    Kate: It's cool Chase, don't worry about it.
<<endif>>
===
title: DaniMomParty
tags:
---
<<if visited("DaniMomParty") is false>>
Abigail: Wow, my two favorite people in the whole world!
Dani: I was just telling your mom how nice it is for us to be all here together again.
Dani: Even without you... It's been a while since we all hung out.
Mom: You've been with each other since you were little kids!
Abigail: It does feel nice
Mom: It's the way it is supposed to be!
Abigail: Mom...
Dani: We know, Abbs, you are leaving again soon. Let's just enjoy today.
Mom: Dani told me all about their time abroad. It sounds very lonely, you know.
Mom: Abigail...
Mom: What if you came back because this is where you are supposed to be?
Abigail: Mom, don't do that
Mom: I'm sorry baby, you know I'll support you no matter what. I'm just worried.
Abigail: Dani's experience isn't my experience, ok mom?
Dani: What is your experience then, Abbs? You barely tell us anything!
Abigail: I don't have it all figured out yet, ok?
Abigail: But that doesn't mean I'll drop everything and come back for good
Dani: I didn't find out there what I was missing here. Did you, Abbs?
Abigail: Things are a mess right now. But I'll fix them.
Abigail: I'll fix them.
Mom: We know you will, baby.
    <<set $peopletalkedto = $peopletalkedto + 1>>
    <<if $peopletalkedto > 2>>
        Gabe:Alright party people! Let's play some Azulejo.
        Joanna: Abigail should judge!
        <<StartPartyAzulejo PartyTransitionManager>>
    <<else>>
        Abigail: I should talk to rest of the gang.
    <<endif>>
<<else>>
    Mom: Maybe I could try setting you up!
    Dani: I think I'm good. Thanks though.
<<endif>>

===
title: GabeJoannaParty
tags:
---
<<if visited("GabeJoannaParty") is false>>
Joanna: Gabe didn't you just get a promotion?
Gabe: I'm co-manager of the newly formed logistics and efficiency department. 
Abigail: There's two managers of the efficiency department?
Gabe: logistics and efficiency Abbi-gal. Also co is just a title.
Gabe: There's five of us and two managers.
Joanna: So what are you actually doing?
Gabe: Well a lot of different stuff but mostly
Gabe: your mom.
Abigail: Gabriel you suck so much. 
Joanna: Hahahahaha
Abigail: No way that made you laugh dude. 
Abigail: Joanna what are you up to?
Joanna: Oh you know finishing up my PHD and advising for the new museum. 
Gabe: It's crazy luck you're here Abbs the museum is opening this week!
Abigail: I don't know how long I'm staying.
Joanna: You're not here for the museum opening? I'm giving a big speech. 
Joanna: Didn't your mom tell you?
Abigail: No um but that's so cool if I'm still around I'll be there. 
Gabe: Of course you're gonna be here it's a huge deal!
    <<set $peopletalkedto = $peopletalkedto + 1>>
    <<if $peopletalkedto > 2>>
        Gabe:Alright party people! Let's play some Azulejo.
        Joanna: Abigail should judge!
        <<StartPartyAzulejo PartyTransitionManager>>
    <<else>>
        Abigail: I should talk to rest of the gang.
    <<endif>>
<<else>>
    Joanna: I really hope you can make it to the museum opening Abigail. 
<<endif>>
===
title: PartyStart
tags:
---
<<declare $peopletalkedto = 0>>
Abigail: Well I might as well say hi to everyone.
===
title: DaniDay1PreAzulejo
tags:
---
Abigail: Hey loser.
Dani: Abigail!
Abigail: Knew I’d find you over here.
Dani: I’m a fan of mesmerizing catastrophes, what can I say?
Abigail: It’s really good to hang out with you again.
Dani: You too Abbs, sorry if it was weird I was at your house this morning.
Abigail: Not at all, I’m glad you’re keeping her company.
Dani: She’s been a lifesaver for me too lately.
Abigail: Working at the restaurant?
Dani: Yeah, it’s not the same without you.
Dani: It’s Abigail this, Abigail that, apparently my table setting doesn’t hold a candle to yours.
Abigail: Hey um…
Abigail: Do you remember this azulejo tile I used to have?
Abigail: It had like, a swirl on it?
Dani: No? What else do you remember about it?
Abigail: I can’t even remember it clearly.
Dani: What made you want to look for it?
Abigail: Before I showed up here, I was closing my eyes to go to sleep
Abigail: And it just popped into my head and it won’t go away.
Dani: Maybe it’s what brought you here.
Abigail: Yeah that’s what I was thinking, that I could find it and then go back and things would be
Abigail: Things would be better.
Dani: Are things bad?
Abigail: Not exactly I just…
-> I think I forgot how to cook 
    Dani: Better not tell my dad that, it will shatter his entire reality.
    Dani: Are things okay with your job?
    Abigail: I think I might have messed it up.
    Dani: You’re pretty good at fixing things?
    Abigail: Maybe, if I’m there to do it.
-> I can’t make anyone laugh 
    Dani: I don’t believe that for a second.
    Abigail: Everyone’s sense of humor there is sharper or meaner or-
    Dani: You’ll figure it out, you always do.
    Abigail: Maybe, if I’m there to do it.
Dani: Well sounds better than my attempt to leave.
Abigail: Do you want to talk about it?
Dani: Could we just, could we play azulejo?
Dani: I feel like that would be easier.
Abigail: Yeah? Do you have chalk?
Dani: Always.
<<PreCollageAzulejoDone SceneController>>
===
title: DaniDay1PostAzulejo
tags:
---
<<if visited("DaniDay1PostAzulejo") is false>>
Dani: I don’t know dude, being there it just really sucked.
Dani: It sucked so much I didn’t even feel like writing about it.
->  At least you tried
    Dani: I feel like I was more hopeful before I tried.
    Dani: Now it’s like I know that I can’t do it.
    Dani: It was a mistake to go.
    Abigail: No it wasn’t. Dani-
    Dani: It’s okay Abbs don’t worry about it.
-> Are you still writing?
    Dani: Of course, I wouldn’t just stop.
    Abigail: No I know, I’m just-
    Dani: I can’t believe you’d ask me that.
    Abigail: I asked because I want to know what’s going on with you.
    Abigail: I didn’t mean-
    Dani: I’m sorry, I shouldn’t have said that Abbs.
    Dani: You just wanted to hear how I'm doing. 
Dani: Have you talked to Kate at all?
Abigail: Not today, no. Is she okay?
Dani: Kate’s always okay. She’s too busy to not be.
Dani: Are you ready to see her later?
Abigail: Why would that be happening?
Dani: Because your mom invited everyone over for dinner?
Abigail: Oh my god.
Dani: Oh and this is gonna be a terrible thing for you to hear but…
Dani: I feel the need to prepare you for the jokes Gabe might make later.
Dani: Kate and Gabe. Uh...they happened.  
Abigail: What??? No.
Dani: Yes.
Dani: It’s over now. She was “too busy for a relationship”, but it happened.
Abigail: Awesome.
<<PostCollageAzulejoDone SceneController>>
<<else>>
Dani: I'll see you at your mom's, Abbs
<<endif>>
===
title: HankCollageNPC
tags:
---
Hank: I used to come to this coffee shop every morning.
Abigail: Well ran dry?
Hank: It ain't even walking friend. 
===
title: JuniorCollageNPC
tags:
---
Junior: I think they're building something!
Abigail: I think it's a landfill. 
Junior: That's still something!
===
title: ChestIntro
tags:
---
<<if visited("ChestIntro") is false>>
Abigail: Hello, chest.
Abigail: Mom said you had some tiles for me.
Chest: <b> HERE ARE YOUR TILES. </b>
Abigail: These?? Really?
Chest: <b> SOME OF YOUR MOM'S FAVORITES. </b>
Abigail: Thank you, I guess
Chest: <b> YOU ARE WELCOME, ABIGAIL </b>
Chest: <b> DON'T FORGET TO PRESS TAB AND CHECK OUT YOUR INVENTORY </b>
Abigail: Why are you talking like that?
Chest: <b> THE ACTIVE TAB WILL LET YOU SORT THE TILES YOU TAKE TO THE GAMES </b>
Chest: <b> THE COLLECTION TAB IS WHERE YOU KEEP ALL OF YOUR OTHER TILES</b>
Abigail: I'm aware, I used to live here!
<<ChestIntroDone SceneController>>
<<else>>
Chest: <b> LONG STORY SHORT: JUST PRESS TAB, ABIGAIL. <b>
<<endif>>
===
title: JulieBurning
tags:
---
<<if visited("JulieBurning") is false>>
Julie: The unconfined movement of the fire, the ever dancing dark smoke.
Julie: I love this place.
Abigail: Weren't they going to put this fire out many years ago?
Julie: Every few months some government official will try to.
Julie: But the proceedings always end up getting caught up in a bureaucratic web.
Abigail: Shouldn't it be simple to put out a fire?
Julie: It's a jurisdictional gray area. A federal building on municipal land, with the firefighters being state employees.
Julie: And with Ochre Bay's water company being privatized a couple years ago
Julie: It also just doesn't make sense financially. 
Abigail: This can't be good for the environment, right?
Julie: Certainly it isn't. But that doesn't mean it isn't the most beautiful view I've ever seen.
<<else>>
Julie: The fire...
Abigail: The fire!
<<endif>>
===
title: HaroldFountain
tags:
---
<<if visited("HaroldFountain") is false>>
Harold: Fascinating! Anita Clark's very own Bathing Baby Fountain!
Harold: I've waited my whole life to see it!
Harold: It has even more presence than the text books led me to believe.
Abigail: Baby? I thought it was a busty woman
Harold: Dear lord...
Abigail: It's just a fountain, dude, it has been here forever
Harold: Precisely! It was completed in 1867, so much history!
Harold: What an exquisite use of form and color. Intensely dynamic!
Abigail: Yeah, I guess it does look kinda rad
Harold: Right??
<<else>>
Harold: Open your eyes and learn your history, young lady
Harold: You will see the world is way more exciting than you think
<<endif>>
===
title: BlockedDoor
tags:
---
Abigail: I don't think I should go this way just yet.
===
title: AbigailOpening
tags:
---
Abigail: You have to be joking. 
Abigail: I'm back here?
Abigail: This can't be happening.
Abigail: I need to get back to the city why did this have to happen this week. 
Abigail: The job interview, lunch with Alice, MY FISH!
Mom: Abigail is that you?!
Abigail: Oh god.
Abigail: Everyone is going to want to see me. 
Abigail: I'm going to have to see everyone. 
Abigail: I'm going to have to see-
Mom: Abigail! Come downstairs! I'm so glad you're home!
<<OpeningDone SceneController>>
===
title: MomDaniPreAzulejo
tages:
---
Mom: Perfect! Those are some of my favorites! The peace flower one reminds me of my younger wilder days if you know what I mean Dani. 
Dani: This hardly seems fair. 
Abigail: Someone's scared!
<<StartAzulejo SceneController>>
===
title: MomDaniPostAzulejo
tages:
---
<<if visited("MomDaniPostAzulejo") is false>>
Mom: Oh well that was just electric!
Dani: I feel cheated. 
Mom: You beat Mrs. Palmer every week Dani have some more tea!
Dani: I think I'm heading out. I'll be down in Wharf this afternoon if you're free, Abbs. 
Abigail: Like I'd go hang with such a sore loser!
Dani: Yeah yeah I was supposed to grab lunch with Chase and his friend but I'm not really feeling it. 
Dani: Go be my stand in, they'll be happier to see you anyway.
Abigail: At the Neptune? 
Dani: He's a creature of herculean habit. 
Abigail: We'll see I need to figure out this plane ticket situation. 
Mom: The travel office is closed on Monday's anyway, Stan likes to go swimming at the beach. Go to the Neptune and see Chase!
<<PostAzulejoDone SceneController>>
<<else>>
Mom: Your hair looks awful! I guess it's fine since you're just going to see Chase at the Neptune.
Mom: But I'll stop and get you some conditioner today.
<<endif>>
===