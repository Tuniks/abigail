title: Friend1
tags:
---
Abigail: Hey, wow you... 
-> Look the same.
	Chase: So much for my new haircut...
	Abigail: I just mean, you look like you, is all.
-> Look so different.
	Abigail:I almost didn't recognize you.
	Chase: It’s just my hair, I’m still me I swear.
Oz: I guess this is the star of the good old days!
Chase: This is Oz, my friend from work.
Abigail: Yeah, super nice to meet you.
Chase: Staff is a little swamped today, you cool with a little wait?
-> Why did you pick this place anyway?
	Abigail: We never used to come here.
	Chase: They have a lava cake that's hella good.
	Chase: And now we can afford it!
	Oz: Plus, Sarah is working today…
	Chase: You would so have Sarah’s work schedule memorized.
	Oz: I don’t have it memorized! I saw her when we were in there!
	Chase: Sure, sure…
	Abigail: Who’s Sarah?
	Oz: Oh, she’s just this girl who…it’s kind of a long story.
-> Sure, sounds good
	Chase: 	Awesome, you are gonna love it.
	Chase: You gotta get the blackberry pancakes! You love those, right?
	Abigail: Not really, I think blackberrys are kinda whatever...
	Chase: No way! We used to pick them every summer.
	Abigail: Maybe that was Oliver...
	Chase: I would never get you and Oliver mixed up.
	Abigail: Have you guys talked lately?
	Chase: Not for a while.
Chase: I almost forgot!
Chase: I was cleaning out my old room the other day and found some of those tiles you gave me.
Abigail: You kept those?
Oz: Wow you had so many. Why didn't you take them when you moved?
Abigail: I’m not really sure? I guess I got a little sick of playing…
Oz: What? How is that even possible?
Chase: I think we should play a quick game, just while we wait.
Oz: Yes! Who's judging?
Chase: You know I love to judge.
Abigail: Okay, yeah, let’s play.
// Example: <<ShowLetter GameManager>>
<<StartTileGame GameManager>>
===
title: OutsideHouse2
tags:
---
Margarette: Hehehe
===
title: Vendor
tags:
---
<<if visited("Vendor") is false>>
Vendor:	I know you from somewhere.
Abigail: 	I know you from somewhere!
Vendor:	We went to tennis camp together.
-> I was really bad at tennis.
	Vendor:	You were really bad at tennis.
-> No, I don’t think that’s it.
	Vendor:	I have the crispest memory of your racket flying through the air after a fit of frustration.
	Vendor:	It soared beautifully.
Abigail: Now you sell Azulejo tiles?
Vendor:	Yeah my dad makes these.
Abigail: Whoa…is that you?
Vendor:	Yeah
Vendor:	...
Vendor:	My dad calls these ones “perfect son” tiles.
Abigail: That’s incredibly sweet.
Vendor:	And a little embarrassing
Abigail: My mom has socks with my face on them. 
Vendor:	...
Abigail: ...
Vendor:	Oh, ok. 
Abigail: I should go, see ya around.
<<else>>
Vendor: Socks...with your face.
<<endif>>
===
title: Mom
tags:
---
<<if visited("Mom") is false>>
Mom: Well if it isn't my favorite daughter! 
Mom: I was just telling Margarette down the road that I miss you, you remember Margarette?
Mom: Her husband got into that terrible accident?
Mom: Do you remember? Poor man hasn't been able to look at a ceiling fan since...
Abigail: Mom! Why am I here?
Mom: Oh look at me going on and on! How are you my dear?
Abigail: I need to get back home.
Mom: Oh well you know how flights are it could be a week.
Abigail: I can't stay here for a week this is the worst time this could've happened. 
Mom: I already told everyone you're here you have a lot of people to see this week!
Abigail: Mom you just found out I was here a few seconds ago
Mom: Oh baby you know me I can't keep a good thing hidden
Abigail: This is a lot I'm going to head to the travel office and see about flights 
Mom: Jim doesn't come in on Mondays
Abigail: Jim? Mom what- I'm going for a walk anyway then. 
Mom: Swing by the cafe! I know Chase would be thrilled to see you!
<<else>>
Mom: Your hair looks a bit dry. It's not a big deal if you're just going to the cafe. 
<<endif>>
===
title: Tourist
tags:
---
<<if visited("Tourist") is false>>
Tourist: These are absolutely blowing my mind! My heart is on fire! My soul is delighted!
Abigail: Yeah these tiles are pretty nice.
Tourist: All the blogs said that getting out of the old town was where you find the real genuine true authentic azulejo tiles. And oh boy were they right!
Abigail: The blogs?
Tourist: That’s how I heard about this city!
Tourist: Azulejo Addicts dot com! You can find it on the world wide web!
Abigail: Huh, yeah thanks I’ll look it up...
<<else>>
Tourist: My pals on the forums are gonna be soooo jealous.
<<endif>>
===
title: OutsideHouse
tags:
---

<<declare $talkedonce = false as bool>>
<<declare $talkedtwice = false as bool>>

<<if visited("OutsideHouse") is false and not $talkedonce>>
    Abigail: Good morning Mrs. Palmer.
    Margarette: Abigail, honey! I knew I would see you today!
    Abigail: Did my mom tell you I was here already?
    Margarette: No, that's not it.
    Margarette: I feel it in my joints when the city changes.
    Margarette: That's how I could tell you would be here.
    Abigail: Oh...
        -> I didn't know I was important enough to be felt in the joints.
        Margarette: Honey, we all are!
        -> And what else can your joints tell me?
        Margarette: That you can't wait to go back to the big city!
        Margarette: Okay, that one your mom did tell me.
        Margarette: Hehehe
    Margarette: But anyway, I don't mean to bother you too much.
    Margarette: A young lady like you must have places to go and people to see!
    Abigail: It was nice seeing you, Mrs. Palmer, I'll catch you later
    Margarette: I will be here waiting!
    <<set $talkedonce = true>>

<<elseif $talkedonce and not $talkedtwice>>
    Margarette: Abigail, honey, you know you are always in my prayers, right?
    Abigail: That's very nice of you, Mrs. Palmer.
    Margarette: Your mom tells me of all of your adventures out there!
    Abigail: All of them?
    Margarette: Every single one!
    Margarette: I am sorry about your last romantic encounter, by the way!
    Margarette: That must've been so embarrassing...
    Abigail: ...
    Margarette: Paolo almost choked laughing with that one!
    Abigail: Oh my god, Mr. Palmer too??
    Margarette: Of course!
    Margarette: Ever since the accident he needs all the distraction he can get!
    Abigail: ...
        -> I am going to assassinate my mother.
        -> I can't show my face around these parts anymore...
    Margarette: Hehehe
    <<set $talkedtwice = true>>

<<elseif $talkedtwice>>
    Margarette: Hehehe
<<endif>>
===

