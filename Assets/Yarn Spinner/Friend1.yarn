title: Friend1
tags:
---
Abigail: Chase! You... 
-> Look the same.
	Chase: So much for my new haircut...
	Abigail: I just mean, you look like you, is all.
-> Have changed so much. 
	Chase: That's what happens when you run away to the big city...
    Abigail: Wow so you really miss me huh?
Oz: Ahoy! I'm here too! This must be the famed Abigail I've heard so much about. 
Abigail: I guess my reputation proceeds me. 
Chase: This is Oz we work together. 
Oz: Pleasure to meet you! Milady. 
Abigail: Yeah ok. It's nice to meet you too Oz.
Chase: Why don't we grab a table and chat.
Oz: Oh and play a game of azulejo?
Abigail: I'm not sure. I might get going... 
Chase: Come on Abbs are you scared of losing?
Abigail: ...
Oz: Wow it just got real. 
Abigail: ...
Chase: One game Abbs and we can get a snack!
Abigail: Chase you're the easiest person to read on Earth I'll play if you judge.
Chase: Deal. 
<<StartTileGame FeltManager>>
===
title: OutsideHouse2
tags:
---
Margarette: Hehehe
===
title: Vendor
tags:
---
<<if visited("Vendor") is false>>
Vendor:	I know you from somewhere.
Abigail: 	I know you from somewhere!
Vendor:	We went to tennis camp together.
-> I was really bad at tennis.
	Vendor:	You were really bad at tennis.
-> No, I don’t think that’s it.
	Vendor:	I have the crispest memory of your racket flying through the air after a fit of frustration.
	Vendor:	It soared beautifully.
Abigail: Now you sell Azulejo tiles?
Vendor:	Yeah my dad makes these.
Abigail: Whoa…is that you?
Vendor:	Yeah
Vendor:	...
Vendor:	My dad calls these ones “perfect son” tiles.
Abigail: That’s incredibly sweet.
Vendor:	And a little embarrassing.
Vendor: You're not allowed to buy one.
Abigail: My mom has socks with my face on them. 
Vendor:	...
Abigail: ...
Vendor:	Oh, ok. 
Abigail: I should go, see ya around.
<<else>>
Vendor: Socks...with your face.
<<endif>>
===
title: Mom
tags:
---
<<if visited("Mom") is false>>
Mom: Well if it isn't my favorite daughter! 
Mom: I was just telling Margarette down the road that I miss you, you remember Margarette?
Mom: Her husband got into that terrible accident?
Mom: Do you remember? Poor man hasn't been able to look at a ceiling fan since...
Mom: Oh look at me going on and on! How are you my dear?
Abigail: I'm not sure how I got here.
Abigail: I was falling asleep, thinking about this tile I used to have. 
Abigail: And now I'm home? 
Mom: I already told everyone you're here you have a lot of people to see this week!
Abigail: Mom you just found out I was here a few seconds ago
Mom: Oh baby you know me I can't keep a good thing hidden
Mom: I think you should go check up on Chase, I'm worried about that boy.
Abigail: This is a lot I'm going to head to the travel office and see about flights.
Mom: Jim doesn't come in on Mondays.
Mom: Swing by the Neptune Cafe! I know Chase would be thrilled to see you!
Mom: You should borrow a few tiles from my collection in case he wants to play Azueljo. 
Mom: They're in that chest over there.
Mom: But not too many! I'm going to play with Margarette later.
<<ExitPossible ExitManager>>
<<else>>
Mom: Your hair looks a bit dry. It's not a big deal if you're just going to the Neptune Cafe. 
<<endif>>
===
title: Tourist
tags:
---
<<if visited("Tourist") is false>>
Tourist: These are absolutely blowing my mind! My heart is on fire! My soul is delighted!
Abigail: Yeah these tiles are pretty nice.
Tourist: All the blogs said that getting out of Acrylic Square where you find the real genuine true authentic azulejo tiles. And oh boy were they right!
Abigail: The blogs?
Tourist: That’s how I heard about this city!
Tourist: Azulejo Addicts dot com! You can find it on the world wide web!
Abigail: Huh, yeah thanks I’ll look it up...
<<else>>
Tourist: My pals on the forums are gonna be soooo jealous.
<<endif>>
===
title: OutsideHouse
tags:
---

<<declare $talkedonce = false as bool>>
<<declare $talkedtwice = false as bool>>

<<if visited("OutsideHouse") is false and not $talkedonce>>
    Abigail: Good morning Mrs. Palmer.
    Margarette: Abigail, honey! I knew I would see you today!
    Abigail: Did my mom tell you I was here already?
    Margarette: No, that's not it.
    Margarette: I feel it in my joints when the city changes.
    Margarette: That's how I could tell you would be here.
    Abigail: Oh...
        -> I didn't know I was important enough to be felt in the joints.
             Margarette: Honey, we all are!
        -> And what else can your joints tell me?
             Margarette: That you can't wait to go back to the big city!
             Margarette: Okay, that one your mom did tell me.
             Margarette: Hehehe
    Margarette: But anyway, I don't mean to bother you too much.
    Margarette: A young lady like you must have places to go and people to see!
    Abigail: It was nice seeing you, Mrs. Palmer, I'll catch you later
    Margarette: I will be here waiting!
    <<set $talkedonce = true>>

<<elseif $talkedonce and not $talkedtwice>>
    Margarette: Abigail, honey, you know you are always in my prayers, right?
    Abigail: That's very nice of you, Mrs. Palmer.
    Margarette: Your mom tells me of all of your adventures out there!
    Abigail: All of them?
    Margarette: Every single one!
    Margarette: I am sorry about your last romantic encounter, by the way!
    Margarette: That must've been so embarrassing...
    Abigail: ...
    Margarette: Paolo almost choked laughing with that one!
    Abigail: Oh my god, Mr. Palmer too??
    Margarette: Of course!
    Margarette: Ever since the accident he needs all the distraction he can get!
    Abigail: ...
        -> I am going to assassinate my mother.
        -> I can't show my face around these parts anymore...
    Margarette: Hehehe
    <<set $talkedtwice = true>>

<<elseif $talkedtwice>>
    Margarette: Hehehe
<<endif>>
===
title: ClayStart
tags:
---
<<if visited("ClayStart") is false>>
Kate: Abigail! It is so wonderful to see your face!
Abigail: Your face is awesome to see as well. Really good face.
Chase: Not weird at all…
Kate: What did you say?
Abigail: Um nothing, I'm going to check out the antique shop!
<<else>>
Chase: Do we think the antique shop owner made the building look like him?
Chase: Or did he make himself look like the building?
Kate: This feels like a chicken or the egg type of situation. 
<<endif>>
===
title: PostAntiqueShop
tags:
---
Chase: You find what you were looking for?
Abigail: Nope. Plus I didn't have any buttons. 
Kate: How’s the big city treating you these days?
Abigail: You know…good…bad…both at once.
Kate: Yeah, totally.
Abigail: How are...how is your life going?
Kate: It’s pretty good these days! I’m working at the library.
Kate: And I’ve been organizing that music festival on the beach, which is so much work but it's gonna be amazing.
Abigail: That’s amazing. You’re amazing.
Chase: Amazing!
Chase: What? I wanted to say it too.
Chase: Good adjective.
Kate: Maybe the best!
Chase:...
Abigail:...
Kate:...
Abigail: You know what, I was just telling Chase how much I wanted to play Azulejo, down for a round?
Chase: I thought you didn't like this version?
Kate: Yeah you used to say this one was too woo woo for you?
Abigail: No, must have me mixed up with someone else. 
<<GoToClayAzulejo ClayManager>>
===
title: AntiqueShopDay1
tags:
---
Shopkeeper: I only accept buttons here. If you don't have any buttons get lost. 
===
title: PostClayAzulejo
tags:
---
Abigail: I should probably get home.
Chase: Sick, we'll come with.
Abigail: Uh, why?
Chase: We’re going to the party at your house.
Kate: Your mom invited the whole gang.
Abigail: Tell me you're joking.
Chase: More time to have completely normal not weird at all conversation
Abigail: I hate you I hate you I hate you.
<<GoToPartyScene ClayManager>>
===
title: PostFeltAzulejo
tags:
---
Oz: And that's another win for Oz!
Chase: Don't rub it in. 
Chase: Abigail's just a little rusty. 
Oz: I gotta dip, duty calls.
Chase: Have fun at the mines!
Oz: I always do
Abigail: Chase, do you remember this one tile, it has kinda like a swirl on it? And it felt like
-> Holding it felt like holding someone’s hand
-> Smelling saltwater in my hair
Chase: No? I don’t think so?
Abigail: I feel like I need to find it.
Chase: We could try looking at the antique shop? 
Chase: I’m headed over there right now to meet Kate, wanna come?
Abigail: My kate?
Chase: Yeah?
Abigail: Um, maybe?
Abigail: Have you heard from Dani at all?
Chase: Dani told me they're hanging around in the Jumble District today?
Chase: You could go say hi if you're not up for seeing Kate. 
Abigail: I thought Dani moved? For that writing program? 
Chase: Yeah, it didn't work out. They got back a few weeks ago.
Chase: Dani might know about the tile you're looking for, and I know they'll want to see you. 
Chase: What's your heart telling you to do?
-> Visit Dani in Jumble District
    Chase: I'll tell Kate you said hi. 
    Abigail: I hate you I hate you I hate you.
    <<GoToCollage FeltManager>> 
-> Hang out with Chase and Kate in The Old Quarter.
    Chase: Okay so I did tell her I would be there 20 minutes ago. 
    Abigail: Classic.
    <<GoToClay FeltManager>>
===
title: WeirdHouse
tags:
---
<<if visited("WeirdHouse") is false>>
HOA President: I am just asking you, no begging you to listen to me!						
HOA President: STOP MAKING YOUR HOUSE LOOK STRANGER!						
HOA President: Every time I leave my house you’ve added a decoration, plant, trim, coat of paint.						
HOA President: It makes my head hurt! It makes my tummy upset!						
Amelia: I don’t know what to tell you. It makes my heart soar!						
HOA President: Every other member of this neighborhood has no problem following the guidelines we’ve outlined?						
Amelia: Oh sweet silly neighbor, comparisons are a waste of your time.		
<<else>>
Amelia: Don't listen to what anyone says house, you look astonishing!
<<endif>>
===
title: PlaygroundKids
tags:
---
<<if visited("PlaygroundKids") is false>>
Ryan 1: Abigail hello!
Abigail: Hey, Ryan! What's up, buddy?
Ryan 2: Who's the old lady, Ryan?
Ryan 1: That's Abigail, she's my neighbor.
Abigail: was*, it's been a while since I moved away
Abigail: And also
Abigail: I'm not an old lady, I'm in my mid-twenties!
Ryan 2: That's old enough to be a grandmother
Abigail: No, it's not?
Ryan 1: I think I've seen grandmothers in their twenties
Abigail: No, You have not. Ryan, who is this clown?
Ryan 2: I'm Ryan
Abigail: No, HE is Ryan
Ryan 2: I'm Ryan too
Ryan 1: He's Ryan too
Ryan 2: Have you never seen two people with the same name?
Abigail: Oh... Is that why you two are friends? Same name?
Ryan 2: No? Do you pick your friends based on their names?
Ryan 1: We are friends because we both like the color purple
Ryan 2: And bugs
Ryan 1: Yeah bugs are awesome
-> I like purple too!
-> I like bugs too!
Ryan 2: Ok, grandma, no one asked
Abigail: Ok, I never asked to be tormented by babies, I'm leaving!
<<else>>
Ryan 1: So, what is your favorite bug?
Ryan 2: Every bug!
Ryan 1: Mine too!!
<<endif>>
===
title: HauntedHouseTeens
tags: 
---
<<if visited("HauntedHouseTeens") is false>>
Albert: I see a poem.
Fellini: I see a film.
Abigail: I see two losers. 
Albert: Abigail! Long time no see! 
Fellini: Art is cool lady. 
Abigail: Sorry, losers non-pejorative. 
Fellini: Pejorateive?
Albert: How've you been Abigail? Do you miss babysitting me?
Abigail: Not at all you were such a weird kid. 
Fellini: What does perjorative mean?
Albert: I was not a weird kid I was, I am, eclectic. 
Abigail: Whatever you say Albert I'll catch you later.
<<else>>
Fellini: Dude what does pejorative mean?
Albert: I don't know! Quit asking me. 
<<endif>>
===
title: KateChaseParty
tags:
---
<<if visited("KateChaseParty") is false>>
Kate: It's nice being back in your house Abigail. 
Abigail: Yeah it's nice having you back in my house.
Kate: Oh Chase! I just remembered I have a favor to ask.
Chase: What’s up?
Kate: Next Wednesday at 11am we’re doing a big load in a day at the venue. 
Kate: We could use some extra hands to move equipment?
Chase: When is this?
Kate: Next Wednesday at 11am.
Chase: Hmm maybe I could...
Kate: It’s okay, no worries.
Chase: If you absolutely can’t find anyone else, let me know.
Kate: I’ll figure it out.
Abigail: You suddenly have a 9-5 or something Chase?
Chase: Me? Never.
Kate: His latest thing has been carpentry.
Chase: I’ve been feeling a lot like Jesus lately.
Abigail: You’ve never been to church in your life.
Chase: But I would look good on a cross!
Chase: If one of you backs me up there could be a new coffee table in your future.
Kate: Not sure if that’s worth it, the birdhouse you made for my birthday was unfortunately short lived.
Chase: I did not build it with a high weight limit!
Chase: No one could have predicted an entire family of squirrels would move in!
Kate: Maybe Jesus could have predicted that…
    <<set $peopletalkedto = $peopletalkedto + 1>>
    <<if $peopletalkedto > 2>>
        Gabe:Alright party people! Let's play some Azulejo.
        Joanna: Abigail should judge!
        <<StartPartyAzulejo PartyTransitionManager>>
    <<else>>
        Abigail: I should talk to rest of the gang.
    <<endif>>
<<else>>
    Chase: I can try repairing the bridhouse if you want?
    Kate: It's cool Chase, don't worry about it.
<<endif>>
===
title: DaniMomParty
tags:
---
<<if visited("DaniMomParty") is false>>
Abigail: Wow, my two favorite people in the whole world!
Abigail: What are you guys chatting about?
Mom: Dani was just telling me about their time living away
Mom: It seems like a very lonely experience
Mom: Your parents must be so happy you decided to come back...
Abigail: Mom.
Mom: I'm joking! It warms my heart seeing you growing and living out your dream!
Mom: But moms get lonely too
Mom: But go on, Dani, tell me: what have you been doing since you came back?
Dani: I've been helping my parents around the restaurant a bit
Dani: Not much else going on...
Mom: That's so sweet!
Mom: And what are the next steps?
Mom: What's in store for one of the most brilliant and kind young minds I know?
Dani: Oh... I don't know,
Dani: I think I'm still figuring things out
Mom: It's okay, honey, you have time! It's a marathon, not a race!
Abigail: Are we done with the interrogation, Mom?
Mom: Afraid I'm gonna steal your friends, Abigail?
Dani: It's ok, Abbs, really
Mom: If you are ever bored, Dani, know that there's a lonely old lady in this house that would love the company!
Mom: And then we can gossip to our heart's content, without the fun police nagging us!
Dani: Haha, thank you, I appreciate it
    <<set $peopletalkedto = $peopletalkedto + 1>>
    <<if $peopletalkedto > 2>>
        Gabe:Alright party people! Let's play some Azulejo.
        Joanna: Abigail should judge!
        <<StartPartyAzulejo PartyTransitionManager>>
    <<else>>
        Abigail: I should talk to rest of the gang.
    <<endif>>
<<else>>
    Mom: Maybe I could try setting you up!
    Dani: I think I'm good. Thanks though.
<<endif>>

===
title: GabeJoannaParty
tags:
---
<<if visited("GabeJoannaParty") is false>>
Joanna: Gabe didn't you just get a promotion?
Gabe: I'm co-manager of the newly formed logistics and efficiency department. 
Abigail: There's two managers of the efficiency department?
Gabe: logistics and efficiency Abbi-gal. Also co is just a title.
Gabe: There's five of us and two managers.
Joanna: So what are you actually doing?
Gabe: Well a lot of different stuff but mostly
Gabe: your mom.
Abigail: Gabriel you suck so much. 
Joanna: Hahahahaha
Abigail: No way that made you laugh dude. 
Joanna: I'm easy to please. 
Gabe: That's what sh-
Abigail: Nope. Try again. 
Joanna: Hahahahahahahahahaha
Joanna: What? Laughing releases endorphins. 
Joanna: I'm just indulging in some natural stress relief!
Abigail: You guys are so embarassing. 
Gabe: You love us right jojo?
Abigail: Big on nicknames today?
Joanna: He's been on a kick lately. 
Abigail: We both can agree Jojo isn't going to stick right?
Joanna: Definitely not. 
    <<set $peopletalkedto = $peopletalkedto + 1>>
    <<if $peopletalkedto > 2>>
        Gabe:Alright party people! Let's play some Azulejo.
        Joanna: Abigail should judge!
        <<StartPartyAzulejo PartyTransitionManager>>
    <<else>>
        Abigail: I should talk to rest of the gang.
    <<endif>>
<<else>>
    Gabe: What do we think of Jujube?
    Joanna: You're getting better at this nickname thing.
<<endif>>
===
title: PartyStart
tags:
---
<<declare $peopletalkedto = 0>>
Abigail: Well I might as well say hi to everyone.
===
title: DaniDay1PreAzulejo
tags:
---
Abigail: Hey loser.
Dani: Abigail?
Dani: What are you doing here?
Abigail: I don’t exactly know.
Abigail: Heard from Chase you got back a few weeks ago?
Dani: Yep. I did indeed.
Abigail: Up to anything special these days?
Dani: Oh you know, on that big journey of self discovery.
Abigail: What have you found out?
Dani: That I’m really good at sitting on park benches.
Dani: And I’m still writing. Promise I haven’t given up yet.
Dani: How long is your fancy job letting you stay for?
Abigail: Only a few days
Abigail: And I think I might have messed that job up.
Dani: Well, you should stay for the Azulejo museum opening in Acrylic Square.
Dani: There’s gonna be this tournament or something.
Abigail: Never took you for a fan of how they play it in that Pollock pisshole.
Dani: Chase calls it that, not me.
Dani: It’s okay, but you know the way I like to play it.
Abigail: Of course I do.
Abigail: Do you have chalk?
Dani: Always.
<<StartCollageAzulejoDay1 CollageManager>>
===
title: DaniDay1PostAzulejo
tags:
---
Dani: So what’s it been like in the big city?
-> Really, really good.
    Dani: I guess I’m not the only one practicing my bench-sitting skills.
    Abigail: No I’m actually doing really good I’m not like…
    Dani: Oh.
    Abigail: No Dani I didn’t mean it like that.
    Dani: You deserve it Abbs don’t feel bad.
-> Living the dream.
    Dani: You know you’re allowed to admit you’re miserable.
    Abigail: I’m not miserable you’re so hyperbolic dude.
    Dani: Haha well are you happy?
    Abigail: There’s something in between isn’t there?
    Dani: I’m not so good at the in betweens.
    Abigail: I know Dani.
Abigail: Anyway I think I might head back home. My mom is probably missing me.
Dani: Please tell me she’s making lasagna.
Abigail: Why do you care?
Dani: Your mom invited everyone over for dinner.
Abigail: Everyone?
Dani: Yeah, Chase, Gabe, Kate, Joanna.
Abigail: Oh my god.
Dani: I mean, I know you and Kate haven’t really…
Abigail: We’ve barely talked. Do you think she hates me?
Dani: Kate could never hate you Abbs.
Abigail: I don’t know about that.
<<GoToPartyCollage CollageManager>>
===
title: HankCollageNPC
tags:
---
Hank: I used to come to this coffee shop every morning.
Abigail: Well ran dry?
Hank: It ain't even walking friend. 
===
title: JuniorCollageNPC
tags:
---
Junior: I think they're building something here!
Abigail: I think it's a landfill. 
Junior: That's still something!
===
title: ChestIntro
tags:
---
Abigail: Hello, chest.
Abigail: Mom said you had some tiles for me.
Chest: <b> HERE ARE YOUR TILES. </b>
Abigail: These?? Really?
Chest: <b> SOME OF YOUR MOM'S FAVORITES. </b>
Abigail: Thank you, I guess
Chest: <b> YOU ARE WELCOME, ABIGAIL </b>
Chest: <b> DON'T FORGET TO PRESS TAB AND CHECK OUT YOUR INVENTORY </b>
Chest: <b> THE ACTIVE TAB WILL LET YOU SORT THE TILES YOU TAKE TO THE GAMES </b>
Chest: <b> THE COLLECTION TAB IS WHERE YOU KEEP ALL OF YOUR OTHER TILES</b>
Abigail: I'm aware, I used to live here!
===

